buildscript {
    ext {
        springVersion = "2.1.1.RELEASE"
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springVersion")
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.11'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'jacoco'

bootJar {
    baseName = 'nhk-easy-daily'
    version =  '0.1.0'
}

jacoco {
    toolVersion = '0.8.2'
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

group 'nhk'
version '1.0-SNAPSHOT'

repositories {
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    implementation 'org.xerial:sqlite-jdbc:3.25.2'
    implementation 'com.github.gwenn:sqlite-dialect:0.1.0'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.8'
    implementation "org.springframework.boot:spring-boot-starter-web:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-freemarker:$springVersion"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa:$springVersion"
    testImplementation "org.springframework.boot:spring-boot-starter-test:$springVersion"
    ktlint "com.github.shyiko:ktlint:0.29.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}